import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.hilt_version = '2.40'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.17.1"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"


        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
    compileSdkVersion = 31
    targetSdkVersion = 31
    minSdkVersion = 21
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task reportMerge(type: ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml")
    // or "reports/detekt/merge.sarif"
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    // The directories where detekt looks for source files.
    // Defaults to `files("src/main/java", "src/test/java", "src/main/kotlin", "src/test/kotlin")`.
    //sources = files("${project.rootDir}/app/src/main/java", "${project.rootDir}/app/src/main/kotlin")
    detekt {
        //toolVersion = "1.19.0"
        parallel = true
        config = files("${project.rootDir}/detekt.yml")

        // Applies the config files on top of detekt's default config file. `false` by default.
        buildUponDefaultConfig = true
    }
    plugins.withType(DetektPlugin) {
        tasks.withType(Detekt) { detektTask ->
            finalizedBy(reportMerge)

            reportMerge.configure { mergeTask ->
                mergeTask.input.from(detektTask.xmlReportFile) // or detektTask.sarifReportFile
            }
        }
    }

}

tasks.withType(Detekt).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
        txt.required.set(true)
        sarif.required.set(true)
    }
}